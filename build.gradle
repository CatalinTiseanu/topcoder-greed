buildscript {
    repositories { mavenCentral() }
    dependencies { classpath 'eu.appsatori:gradle-fatjar-plugin:0.2' }
}

apply plugin: 'java'
apply plugin: 'fatjar'

group = 'greed'
version = file('version').text.trim()

repositories {
    mavenCentral() 
}

dependencies {
    compile 'com.typesafe:config:0.5.0'
    compile files('lib/jmte-3.1-mod.jar')
    compile files('lib/ContestApplet.jar'), {
        ext { fatJarExclude = true }
    }
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'junit:junit:4.9'
}

// Fat jar tricks to include "version" files
fatJar {
    from 'version'
}

// Dist task
task dist(dependsOn: ['clean', 'test', 'fatJar']) {
    description = 'Clean, build, and copy the package to the dist folder'
    doLast {
        copy {
            from 'build/libs'
            into 'dist'
            include '*.jar'
            rename { it.replace('topcoder-greed', 'Greed') }
        }
    }
}

// Gradle Wrapper
task wrapper(type: Wrapper) {
    description = 'Create gradle wrapper'
    gradleVersion = '1.8'
    jarFile = 'gradle/wrapper.jar'
}
